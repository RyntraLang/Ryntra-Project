grammar Ryntra;

// Keywords
PUBLIC: 'public';
INT: 'int';
LONG: 'long';
VOID: 'void';
RETURN: 'return';
IF: 'if';
ELSE: 'else';
WHILE: 'while';
FOR: 'for';
BREAK: 'break';
CONTINUE: 'continue';
BOOL: 'bool';
TRUE: 'true';
FALSE: 'false';
NULL: 'null';
NEW: 'new';
DELETE: 'delete';
REF: 'ref';
PTR: 'ptr';
FN: 'Fn';
UNSAFE: 'unsafe';
FIXED: 'fixed';
STRUCT: 'struct';
SELF: 'self';

// Symbols & Operators
SEMICOLON: ';';
COMMA: ',';
DOT: '.';
LPAREN: '(';
RPAREN: ')';
LBRACE: '{';
RBRACE: '}';
LBRACK: '[';
RBRACK: ']';
INC: '++';
DEC: '--';
ASSIGN: '=';
ADD_ASSIGN: '+=';
SUB_ASSIGN: '-=';
MUL_ASSIGN: '*=';
DIV_ASSIGN: '/=';
MOD_ASSIGN: '%=';
PLUS: '+';
MINUS: '-';
MUL: '*';
DIV: '/';
MOD: '%';

// Bitwise Operators
BIT_AND: '&';
BIT_OR: '|';

// Conditional/Logical Operators
COND_AND: '&&';
COND_OR: '||';
BIT_XOR: '^';
BIT_NOT: '~';
NOT: '!';
SHL: '<<';

// Comparison Operators
EQ: '==';
NE: '!=';
GE: '>=';
LE: '<=';
GT: '>';
LT: '<';

// Bitwise Compound Assignment
AND_ASSIGN: '&=';
OR_ASSIGN: '|=';
XOR_ASSIGN: '^=';
SHL_ASSIGN: '<<=';
SHR_ASSIGN: '>>=';

// Lexical Objects
IDENTIFIER: [a-zA-Z_][a-zA-Z_0-9]*;
STRING_LITERAL: '"' (~["\\\r\n] | '\\' .)* '"';
INTEGER_LITERAL: [0-9]+ [Ll]?;

LINE_COMMENT: '//' ~[\r\n]* -> skip;
BLOCK_COMMENT: '/*' .*? '*/' -> skip;
WS: [ \t\r\n]+ -> skip;

// Parser Rules

program
    : (functionDefinition | structDefinition)+ EOF
    ;

functionDefinition
    : PUBLIC typeSpecifier IDENTIFIER LPAREN parameterList? RPAREN block
    ;

constructor
    : visibilityModifier IDENTIFIER LPAREN parameterList? RPAREN block
    ;

structDefinition
    : annotation* PUBLIC STRUCT IDENTIFIER LBRACE structMember* RBRACE
    ;

structMember
    : visibilityModifier typeSpecifier IDENTIFIER (ASSIGN expression)? SEMICOLON
    | functionDefinition
    | constructor
    ;

visibilityModifier
    : PUBLIC
    ;

annotation
    : LBRACK IDENTIFIER (LPAREN argumentList RPAREN)? RBRACK
    ;

parameterList
    : parameter (COMMA parameter)*
    ;

parameter
    : typeSpecifier IDENTIFIER
    ;

typeSpecifier
    : INT
    | LONG
    | VOID
    | BOOL
    | IDENTIFIER
    | REF LT typeSpecifier GT
    | PTR LT typeSpecifier GT
    | FN LT typeSpecifier LPAREN functionTypeParamList? RPAREN GT
    | typeSpecifier LPAREN functionTypeParamList? RPAREN
    ;

functionTypeParamList
    : typeSpecifier (COMMA typeSpecifier)*
    ;

block
    : LBRACE statement* RBRACE
    ;

statement
    : variableDeclaration SEMICOLON
    | arrayDeclaration
    | expression SEMICOLON
    | returnStatement
    | ifStatement
    | whileStatement
    | forStatement
    | breakStatement
    | continueStatement
    | unsafeBlock
    | fixedStatement
    | DELETE expression SEMICOLON
    ;

fixedStatement
    : FIXED LPAREN PTR LT typeSpecifier GT IDENTIFIER ASSIGN expression RPAREN block
    ;

unsafeBlock
    : UNSAFE block
    ;

whileStatement
    : WHILE LPAREN expression RPAREN block
    ;

forStatement
    : FOR LPAREN forInitClause? SEMICOLON forCondClause? SEMICOLON forOperClause? RPAREN block
    ;

forInitClause
    : variableDeclaration
    | expression
    ;

forCondClause
    : expression
    ;

forOperClause
    : expression
    ;

breakStatement
    : BREAK SEMICOLON
    ;

continueStatement
    : CONTINUE SEMICOLON
    ;

ifStatement
    : IF LPAREN expression RPAREN block elseBranch?
    ;

elseBranch
    : ELSE block
    | ELSE ifStatement
    ;

variableDeclaration
    : typeSpecifier IDENTIFIER (ASSIGN expression)?
    ;

arrayDeclaration
    : typeSpecifier LBRACK RBRACK IDENTIFIER ASSIGN NEW typeSpecifier LBRACK expression RBRACK SEMICOLON
    ;

returnStatement
    : RETURN expression? SEMICOLON
    ;

expression
    : REF LPAREN expression RPAREN                                  # RefExpression
    | PTR LPAREN expression RPAREN                                  # PtrExpression
    | NEW typeSpecifier                                             # NewExpression
    | NEW typeSpecifier LPAREN expression RPAREN                    # NewWithInitExpression
    | LPAREN typeSpecifier RPAREN expression                        # CastExpression
    | LPAREN expression RPAREN                                      # ParenthesizedExpression
    | expression INC                                                # PostfixIncExpression
    | expression DEC                                                # PostfixDecExpression
    | IDENTIFIER LPAREN argumentList? RPAREN                        # FunctionCall
    | object=expression DOT IDENTIFIER LPAREN argumentList? RPAREN  # MethodCallExpression
    | object=expression DOT IDENTIFIER                              # MemberAccessExpression
    | array=expression LBRACK index=expression RBRACK               # ArrayIndexAccess
    | INC expression                                                # PrefixIncExpression
    | DEC expression                                                # PrefixDecExpression
    | MINUS expression                                              # UnaryMinusExpression
    | BIT_NOT expression                                            # UnaryExpression
    | NOT expression                                                # NotExpression
    | left=expression op=(MUL|DIV|MOD) right=expression              # MulDivModExpression
    | left=expression op=(PLUS|MINUS) right=expression               # PlusMinusExpression
    | left=expression op=SHL right=expression                        # ShiftExpression
    | left=expression GT GT right=expression                         # ShiftExpression
    | left=expression op=BIT_AND right=expression                    # BitAndExpression
    | left=expression op=BIT_XOR right=expression                    # BitXorExpression
    | left=expression op=BIT_OR right=expression                     # BitOrExpression
    | left=expression op=(EQ|NE|GE|LE|GT|LT) right=expression         # ComparisonExpression
    | left=expression op=COND_AND right=expression                   # ConditionalAndExpression
    | left=expression op=COND_OR right=expression                    # ConditionalOrExpression
    | <assoc=right> left=expression op=(ASSIGN|ADD_ASSIGN|SUB_ASSIGN|MUL_ASSIGN|DIV_ASSIGN|MOD_ASSIGN|AND_ASSIGN|OR_ASSIGN|XOR_ASSIGN|SHL_ASSIGN|SHR_ASSIGN) right=expression  # AssignmentExpression
    | IDENTIFIER                                                    # VariableReference
    | SELF                                                          # SelfReference
    | STRING_LITERAL                                                # StringLiteral
    | INTEGER_LITERAL                                               # IntegerLiteral
    | TRUE                                                          # TrueLiteral
    | FALSE                                                         # FalseLiteral
    | NULL                                                          # NullLiteral
    ;

argumentList
    : expression (COMMA expression)*
    ;
